ext.module_name = "librarytemplate"

apply from: rootProject.file("gradle/android_library_config.gradle")
apply from: rootProject.file("library_info.gradle")

task checkBintrayConfig {
  doLast {
    if (BINTRAY_ORG.isEmpty() ||
            BINTRAY_USER.isEmpty() ||
            BINTRAY_API_KEY.isEmpty()) {
      def cause = new Exception(
              "BINTRAY_ORG (\"$BINTRAY_ORG\"), BINTRAY_USER (\"$BINTRAY_USER\"), or " +
                      "BINTRAY_API_KEY (\"$BINTRAY_API_KEY\") are not set in your environment variables.")
      throw new TaskExecutionException(it, cause)
    }
    if (library.publish_group == "com.afollestad" &&
            module_name == "librarytemplate") {
      def cause = new Exception(
              "The publish_group is com.afollestad and the module_name is librarytemplate. You should " +
                      "set these to your own values in library_info.gradle and library/build.gradle.")
      throw new TaskExecutionException(it, cause)
    }
  }
}
dependencies {
  compileOnly deps.androidx.annotations
  implementation deps.kotlin.stdlib8

  testImplementation deps.kotlin.test.mockito
  testImplementation deps.test.robolectric
  testImplementation deps.test.junit
  testImplementation deps.test.mockito_core
  testImplementation deps.test.truth
}

publish {
  userOrg =library.userOrg
  bintrayUser = BINTRAY_USER
  bintrayKey = BINTRAY_API_KEY
  userOrg = BINTRAY_ORG
  groupId = library.publish_group
  artifactId = module_name
  publishVersion = library.publish_version
  desc = library.description
  website = library.website
  dryRun = false
}